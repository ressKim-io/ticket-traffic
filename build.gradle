plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.0' apply false
    id 'io.spring.dependency-management' version '1.1.4' apply false
    id 'nu.studer.jooq' version '9.0' apply false
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'

    group = 'com.sportstix'
    version = '1.0.0'

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.boot:spring-boot-dependencies:3.3.0'
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // Prometheus metrics for all services with actuator
        runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

        // OpenAPI documentation
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    tasks.withType(JavaCompile) {
        options.release = 21
        options.compilerArgs += ['-parameters']
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacoco {
        toolVersion = '0.8.11'
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = true
            html.required = true
        }
        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        '**/jooq/generated/**',
                        '**/dto/**',
                        '**/config/**',
                        '**/mapper/**',
                        '**/*Application*'
                ])
            }))
        }
    }
}
